data Bot : Type where end

data Unit : Type where
| tt : Unit
end

data Bool : Type where
| true,false : Bool
end

data Pair (A : Type) (B (a : A) : Type) :: Type where
| pair (a : A) (b : B a) : Pair A B
end

def SPair (A,B : Type) : Type where
(A,B : Type) -> Pair A ((a : A) -> B)
end

data Desc (O : Type) :: Type where
| End (o : O) : Desc O
| Arg (A : Type) (B (a : A) : Desc O) : Desc O
| Rec (A : Type) (D (i (a : A) : O) : Desc O) : Desc O
end

mutual

data Data {O : Type} (D : Desc O) :: Type where
| con (xs : Data' D D) : Data D
end

def Data' {O : Type} (R,D : Desc O) : Type where
| R (End o) = Unit
| R (Arg A D) = Pair A ((a : *) -> Data' R (D a))
| R (Rec A D) = Pair ((a : A) : Data R) ((f : *) -> Data' R (D ((a : A) -> fun R (f a))))
end

def fun {O : Type} (D : Desc O) (x : Data D) : O where
| D (con xs) = fun' D D xs
end

def fun' {O : Type} (R,D : Desc O) (xs : Data' R D) : O where
| R (End o) tt = o
| R (Arg A D) (pair a xs) = fun' R (D a) xs
| R (Rec A D) (pair f xs) = fun' R (D ((a : A) -> fun R (f a))) xs
end

end

mutual

data `Type : Type where
| `Bot,`Unit,`Bool : `Type
| `Pair,`Fun (A : `Type) (B (a : ToType A) : `Type) : `Type
| `Data {O : `Type} (D : `Desc O) : `Type
end

def ToType (A : `Type) : Type where
| `Bot = Bot
| `Unit = Unit
| `Bool = Bool
| (`Pair A B) = Pair (ToType A) ((a : ToType A) -> ToType (B a))
| (`Fun A B) = (a : ToType A) : ToType (B a)
| (`Data D) = Data (ToDesc D)
end

data `Desc (O : `Type) :: Type where
| `End (o : ToType O) : `Desc O
| `Arg (A : `Type) (D (a : ToType A) : `Desc O) : `Desc O
| `Rec (A : `Type) (D (o (a : ToType A) : ToType O) : `Desc O) : `Desc O
end

def ToDesc {O : `Type} (D : `Desc O) : Desc (ToType O) where
| (`End o) = End o
| (`Arg A D) = Arg (ToType A) ((a : *) -> ToDesc (D a))
| (`Rec A D) = Rec (ToType A) ((o : *) -> ToDesc (D o))
end

end

def DataArg {O : `Type} {A : `Type} (R : `Desc O) (D (a : ToType A) : `Desc O) (a : ToType A) : Type where
| R D a = Data' (ToDesc R) (ToDesc (D a))
end

def DataRec {O : `Type} {A : `Type} (R : `Desc O)
(D (o (a : ToType A) : ToType O) : `Desc O)
(f (a : ToType A) : Data (ToDesc R))
: Type where
| R D f = Data' (ToDesc R) (ToDesc (D ((a : *) -> fun (ToDesc R) (f a)))) 
end

mutual

data Path (A : `Type) (a : ToType A) : Type where
| here {A : `Type} {a : ToType A} : Path A a
| thereFun {A : `Type} {B (a : ToType A) : `Type}
  {f (a : ToType A) : ToType (B a)}
  (g (a : ToType A) : Path (B a) (f a))
  : Path (`Fun A B) f
| thereData {O : `Type} {D : `Desc O}
  {xs : Data' (ToDesc D) (ToDesc D)}
  (i : Path' D D xs)
  : Path (`Data D) (con xs)
end

data Path' {O : `Type} (R : `Desc O) :: (D : `Desc O) (xs : Data' (ToDesc R) (ToDesc D)) : Type where
| thereArg1 {A : `Type} {D (a : ToType A) : `Desc O}
  {a : ToType A} {xs : DataArg R D a}
  (i : Path A a)
  : Path' R (`Arg A D) (pair a xs)
| thereArg2 {A : `Type} {D (a : ToType A) : `Desc O}
  {a : ToType A} {xs : DataArg R D a}
  (i : Path' R (D a) xs)
  : Path' R (`Arg A D) (pair a xs)
| thereRec1 {A : `Type} {D (o (a : ToType A) : ToType O) : `Desc O}
  {f (a : ToType A) : Data (ToDesc R)} {xs : DataRec R D f}
  (g (a : ToType A) : Path (`Data R) (f a))
  : Path' R (`Rec A D) (pair f xs)
| thereRec2 {A : `Type} {D (o (a : ToType A) : ToType O) : `Desc O}
  {f (a : ToType A) : Data (ToDesc R)} {xs : DataRec R D f}
  (i : Path' R * xs)
  : Path' R (`Rec A D) (pair f xs)
end

end

